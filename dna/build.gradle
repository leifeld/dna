// Java plugin
plugins {
    id 'java'
}

// create jar file, not just .class files
jar {
    inputs.dir '.'

    // point the manifest to the right class
    manifest {
        attributes 'Main-Class': 'dna.Dna'
    }
    
    // make a fat jar
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    
    // when creating fat jar, overwrite license files when unpacking jars if they already exist
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    
    // parse DNA version number and add to jar file name
    new File(project(':dna').projectDir.toString() + '/src/main/java/dna/Dna.java').eachLine { line ->
        if (line ==~ /.*String version.*/) {
            def matches = (line =~ /\d+\.\d+\.\d+/)
            archiveVersion = matches[0]
        }
    }
    
    sourceCompatibility = '11'
    targetCompatibility = '11'

    destinationDirectory.set(file("$rootDir/build/"))
}

// set up Maven repository for the jar dependencies
repositories {
    mavenCentral()
}

// download jar files
dependencies {
    // https://mvnrepository.com/artifact/commons-logging/commons-logging
    implementation group: 'commons-logging', name: 'commons-logging', version: '1.2'
    // https://mvnrepository.com/artifact/org.apache.commons/commons-pool2
    implementation group: 'org.apache.commons', name: 'commons-pool2', version: '2.11.1'
    // https://mvnrepository.com/artifact/com.google.code.gson/gson
    implementation group: 'com.google.code.gson', name: 'gson', version: '2.9.0'
    // https://mvnrepository.com/artifact/com.zaxxer/HikariCP
    implementation group: 'com.zaxxer', name: 'HikariCP', version: '5.0.1'
    // https://mvnrepository.com/artifact/org.jasypt/jasypt
    implementation group: 'org.jasypt', name: 'jasypt', version: '1.9.3'
    // https://mvnrepository.com/artifact/org.nuiton.thirdparty/JRI
    implementation group: 'org.nuiton.thirdparty', name: 'JRI', version: '0.9-9'
    // https://mvnrepository.com/artifact/com.github.lgooddatepicker/LGoodDatePicker
    implementation group: 'com.github.lgooddatepicker', name: 'LGoodDatePicker', version: '11.2.1'
    // https://mvnrepository.com/artifact/mysql/mysql-connector-java
    implementation group: 'mysql', name: 'mysql-connector-java', version: '8.0.28'
    // https://mvnrepository.com/artifact/org.postgresql/postgresql
    implementation group: 'org.postgresql', name: 'postgresql', version: '42.3.3'
    // https://mvnrepository.com/artifact/org.slf4j/slf4j-api
    implementation group: 'org.slf4j', name: 'slf4j-api', version: '2.0.0-alpha7'
    // https://mvnrepository.com/artifact/org.slf4j/slf4j-nop
    implementation group: 'org.slf4j', name: 'slf4j-nop', version: '2.0.0-alpha7'
    // https://mvnrepository.com/artifact/org.xerial/sqlite-jdbc
    implementation group: 'org.xerial', name: 'sqlite-jdbc', version: '3.36.0.3'
    // https://mvnrepository.com/artifact/org.swinglabs.swingx/swingx-all
    implementation group: 'org.swinglabs.swingx', name: 'swingx-all', version: '1.6.5-1'
    // https://mvnrepository.com/artifact/xmlpull/xmlpull
    implementation group: 'xmlpull', name: 'xmlpull', version: '1.1.3.4d_b4_min'
    // https://mvnrepository.com/artifact/org.ogce/xpp3
    implementation group: 'org.ogce', name: 'xpp3', version: '1.1.6'
    // https://mvnrepository.com/artifact/com.thoughtworks.xstream/xstream
    implementation group: 'com.thoughtworks.xstream', name: 'xstream', version: '1.4.19'
    // https://mvnrepository.com/artifact/org.jdom/jdom
	implementation group: 'org.jdom', name: 'jdom', version: '1.1.3'
    // https://mvnrepository.com/artifact/me.tongfei/progressbar
    implementation group: 'me.tongfei', name: 'progressbar', version: '0.9.4'
    // https://mvnrepository.com/artifact/org.ojalgo/ojalgo
    implementation group: 'org.ojalgo', name: 'ojalgo', version: '51.4.1'
    // https://mvnrepository.com/artifact/org.apache.commons/commons-math3
    implementation group: 'org.apache.commons', name: 'commons-math3', version: '3.6.1'
}
